buildscript {
    ext.jackson_version = '2.16.1'
    ext.kotlin_version = '1.9.23'
}

plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "maven-publish"
    id "signing"
}

group = "io.github.mslenc"
version = "0.30.4"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://mslenc-releases.s3.amazonaws.com/maven2" }
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    api "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"

    implementation("io.github.mslenc:utilsktx:1.5.3")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"

    // https://mvnrepository.com/artifact/io.github.lukehutch/fast-classpath-scanner
    implementation "io.github.lukehutch:fast-classpath-scanner:2.21"

    implementation "io.pebbletemplates:pebble:3.2.2"

    testImplementation("junit:junit:4.13.2")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'gqlktx'
            from components.java

            pom {
                name = "gqlktx"
                description = "A GraphQL executor for Kotlin."
                url = "https://github.com/mslenc/gqlktx"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mslenc'
                        name = 'Mitja Slenc'
                        email = 'mslenc@gmail.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/mslenc/gqlktx.git"
                    developerConnection = "scm:git:git@github.com:mslenc/gqlktx.git"
                    url = "https://github.com/mslenc/gqlktx"
                }
            }
        }
    }

    repositories {
        maven {
            url = "s3://mslenc-releases/maven2"
            credentials(AwsCredentials) {
                accessKey = awsAccessKeyMslenc
                secretKey = awsSecretKeyMslenc
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

